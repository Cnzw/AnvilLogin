plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
}

group = pluginGroup
version = pluginVersion

java.sourceCompatibility = JavaVersion.VERSION_16
java.targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        name = 'codemc-repo'
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }

    maven { url "https://erethon.de/repo/" }
    maven { url = 'https://jitpack.io' }

    maven {
        name = 'destroystokyo-repo'
        url = 'https://repo.destroystokyo.com/repository/maven-public/'
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    flatDir { dirs 'libraries' } // FastLogin
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.5'

    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'fr.xephi:authme:5.6.0-SNAPSHOT'
    compileOnly name: 'FastLoginBukkit'

    // implementation 'net.wesjd:anvilgui:1.5.1-SNAPSHOT' // anvilgui // using codemc repo worst thing ever
    implementation name: 'anvilgui-1.5.1'
    implementation 'com.gitlab.samb440:languagy:2.0.3-RELEASE' // languagy
}

tasks.shadowJar {
    relocate 'net.islandearth.languagy', 'net.islandearth.anvillogin.languagy'
    relocate 'net.wesjd.anvilgui', 'net.islandearth.anvillogin.anvilgui'
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Force warnings
    options.compilerArgs << '-Xlint:all'
    options.deprecation = true
}

build.dependsOn shadowJar

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [version: version]
}